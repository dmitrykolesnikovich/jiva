subprojects {

    ext.commonExists = { file("$projectDir/src/common").exists() }
    ext.androidExists = { file("$projectDir/src/android").exists() }
    ext.desktopExists = { file("$projectDir/src/desktop").exists() }
    ext.iosExists = { file("$projectDir/src/ios").exists() }
    ext.jsExists = { file("$projectDir/src/js").exists() }
    ext.jvmExists = { file("$projectDir/src/jvm").exists() }
    ext.isCrossplatform = { !androidExists() && !jsExists() && !iosExists() && !desktopExists() && !jvmExists() }

    ext.artifact = { String artifactId ->
        if (project.path.endsWith("-draft")) return // quickfix todo improve
        if (project.path.endsWith("-examples")) return // quickfix todo improve
        if (artifactId.startsWith(":")) artifactId = artifactId.substring(1) // quickfix todo improve
        if (artifactId.count(":") != 1) error("Invalid artifact id: $artifactId")

        project.ext.artifactId = artifactId
        project.ext.artifactGroup = artifactId.split(":")[0]
        project.ext.artifactName = artifactId.split(":")[1]
        project.ext.packageId = artifactId.replace(":", ".").replace("-", ".")
        project.ext.packagePath = project.packageId.replaceAll("\\.", "/")
        project.ext.isTestProject = project.path.endsWith("-test")

        /*interop*/

        if (artifactName.startsWith("android-")) {
            project.android_interop_library()
        } else if (artifactName.startsWith("desktop-")) {
            project.desktop_interop_library()
        } else if (artifactName.startsWith("ios-")) {
            project.ios_interop_library()
        } else if (artifactName.startsWith("js-")) {
            project.js_interop_library()
        }

        /*launchers*/

        else if (artifactName.endsWith("-android")) {
            project.android_launcher()
        } else if (artifactName.endsWith("-desktop")) {
            project.desktop_launcher()
        } else if (artifactName.endsWith("-ios")) {
            project.ios_launcher()
        } else if (artifactName.endsWith("-js")) {
            project.js_launcher()
        }

        /*mpp*/

        else {
            project.common_library()

            if (jvmExists()) project.jvm_library()
            if (desktopExists() || isCrossplatform()) project.desktop_library()
            if (androidExists() || isCrossplatform()) project.android_library()
            if (iosExists() || isCrossplatform()) project.ios_library()
            if (jsExists() || isCrossplatform()) project.js_library()

            // >> quickfix todo conceptualize
            if (commonExists() && desktopExists()) {
                // used by "featurea:config", "featurea:modbus"
                project.android_library()
                project.ios_library()
                project.js_library()
            }
            if (commonExists() && jvmExists()) {
                // used by "featurea:jvm"
                project.ios_library()
                project.js_library()
            }
            // <<
        }
    }

}
